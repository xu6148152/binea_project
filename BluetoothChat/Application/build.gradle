
buildscript {
    repositories {
//        jcenter()
        mavenCentral()
        maven {
            url 'https://repos.zeroturnaround.com/nexus/content/repositories/zt-public-releases'
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.0'
        // This does not break the build when Android Studio is missing the JRebel for Android plugin.
        classpath 'com.zeroturnaround.jrebel.android:jr-android-gradle:0.8.+'
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.7.0'
    }
}

apply plugin: 'com.android.application'
// This does not break the build when Android Studio is missing the JRebel for Android plugin.
apply plugin: 'com.zeroturnaround.jrebel.android'

apply plugin: 'com.google.protobuf'

repositories {
    jcenter()
}

dependencies {
    compile 'com.android.support:support-v4:21.0.2'
    compile 'com.android.support:gridlayout-v7:21.0.2'
    compile 'com.android.support:cardview-v7:21.0.2'
    compile files('src/main/lib/lib94fifty-1.0.624.jar')
    compile 'com.android.support:percent:23.0.0'

    compile 'com.google.protobuf.nano:protobuf-javanano:3.0.0-alpha-3'
    compile 'io.grpc:grpc-protobuf-nano:0.7.0'
}

// The sample build uses multiple directories to
// keep boilerplate and common code separate from
// the main sample code.
List<String> dirs = [
    'main',     // main sample code; look here for the interesting stuff.
    'common',   // components that are reused by multiple samples
    'template'] // boilerplate code that is generated by the sample template process

android {
    compileSdkVersion 23
    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 22
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    sourceSets {
        main {
            java {
                dirs.each { dir ->
                    java.srcDirs "src/${dir}/java"
                    res.srcDirs "src/${dir}/res"
                }
            }

//            proto {
//                dirs.each { dir ->
//                    proto.srcDirs "src/${dir}/proto"
//                }
//            }

        }
        androidTest.setRoot('tests')
        androidTest.java.srcDirs = ['tests/src']

    }

}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0-alpha-3'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:0.7.0'
        }
    }
    generateProtoTasks {
        all()*.builtins {
            javanano {
                // Options added to --javanano_out
                option 'java_multiple_files=false'
                option 'ignore_services=true'
            }
        }
        ofNonTest()*.plugins {
            grpc {
                // Options added to --grpc_out
                option 'nano=true'
            }
        }
    }
}















